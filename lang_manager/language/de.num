__numbertext__
^0 null
1$ eins
1 ein
2 zwei
3 drei
4 vier
5 fünf
6 sechs
7 sieben
8 acht
9 neun
10 zehn
11 elf
12 zwölf
16 sechzehn
17 siebzehn
1(\d) $1zehn
20 zwanzig
2(\d) $1undzwanzig
30 dreißig
3(\d) $1unddreißig
60 sechzig
6(\d) $1undsechzig
70 siebzig
7(\d) $1undsiebzig
(\d)0 $1zig
(\d)(\d) $2und$1zig
#1(\d\d) hundert$1
(\d)(\d\d) $1hundert$2
#1(\d{3}) tausend$1
(\d{1,3})(\d{3}) $1tausend$2
1(\d{6}) eine Million $1
(\d{1,3})(\d{6}) $1 Millionen $2
1(\d{9}) eine Milliarde $1
(\d{1,3})(\d{9}) $1 Milliarden $2
1(\d{12}) eine Billion $1
(\d{1,3})(\d{12}) $1 Billionen $2
1(\d{15}) eine Billiarde $1
(\d{1,3})(\d{15}) $1 Billiarden $2
1(\d{18}) eine Trillion $1
(\d{1,3})(\d{18}) $1 Trillionen $2
1(\d{21}) eine Trilliarde $1
(\d{1,3})(\d{21}) $1 Trilliarden $2

# negative number

[-−](\d+) minus |$1

# decimals

"([-−]?\d+)[.,]" $1| Komma
"([-−]?\d+[.,]\d*)(\d)" $1| |$2

# ordinal numbers

"ord ([-−]?[024569])" $1te
"ord ([-−]?\d*0[24569])" $1te
"ord ([-−]?\d*1\d)" $1te
"ord ([-−]?\d+)" $(ord:$1)

ord:(.*)eins	\1erste
ord:(.*)drei	\1dritte
ord:(.*)sieben	\1siebte
ord:(.*)acht	\1achte
"ord:(.*)eine Milli(on|ard)e?"	\1einmilli\2ste
"ord:(.*)eine Billi(on|ard)e?"	\1einbilli\2ste
"ord:(.*)eine Trilli(on|ard)e?"	\1eintrilli\2ste
"ord:(.*) Milli(on|ard)en"	\1milli\2ste
"ord:(.*) Billi(on|ard)en"	\1billi\2ste
"ord:(.*) Trilli(on|ard)en"	\1trilli\2ste
"(ord:.*) "	$1
ord:(.*)	\1ste

help Functions: ord \(ordinal numbers\)