__numbertext__
^0 zéro
1 un
2 deux
3 trois
4 quatre
5 cinq
6 six
7 sept
8 huit
9 neuf
10 dix
11 onze
12 douze
13 treize
14 quatorze
15 quinze
16 seize
20 vingt
30 trente
40 quarante
50 cinquante
60 soixante
71 soixante et onze
7(\d) soixante-$(1\1)
80$ quatre-vingts
80 quatre-vingt
81 quatre-vingt-un
9(\d) quatre-vingt-$(1\1)
(\d)1 $(\10) et un
(\d)(\d) $(\10)-$2
1(\d\d) cent $1
(\d)00$ $1 cents
(\d)(\d\d) $1 cent $2
1100 onze cents
11(\d\d) onze cent $1
1(\d{3}) mille $1
(\d{1,3})(\d{3}) $1 mille $2
1(\d{6}) un million $1
(\d{1,3})(\d{6}) $1| millions $2
1(\d{9}) un milliard $1
(\d{1,3})(\d{9}) $1| milliards $2
1(\d{12}) un billion $1
(\d{1,3})(\d{12}) $1| billions $2
1(\d{15}) un billiard $1
(\d{1,3})(\d{15}) $1| billiards $2
1(\d{18}) un trillion $1
(\d{1,3})(\d{18}) $1| trillions $2
1(\d{21}) un trilliard $1
(\d{1,3})(\d{21}) $1| trilliards $2
1(\d{24}) un quadrillion $1
(\d{1,3})(\d{24}) $1| quadrillions $2

# negative number

[-−](\d+) moins |$1

# decimals

"([-−]?\d+)[.,]" "$1| virgule"
"([-−]?\d+[.,]0*)(\d+)" $1 |$2

# ordinal numbers

"ordf 1" première
"ordm? 1" premier

"ord[fm]? ([-−]?\d+)" $(ord:|$1)

ord:(.*)e \1ième	# quatre etc.
ord:(.*)f \1vième	# neuf
ord:(.*q) \1uième	# cinq
"ord:(.*[^ ]) *" \1ième		# others

help Functions: ord (ordinal numbers)\nordf (feminine ordinal numbers)\nordm (masculine ordinal numbers)
